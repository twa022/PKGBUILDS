# Maintainer:  twa022 <twa022 at gmail dot com>

_pkgname=libva
pkgname=${_pkgname}1
pkgver=1.8.3
pkgrel=2
pkgdesc='Video Acceleration (VA) API for Linux (old version for Steam)'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('libdrm' 'libgl' 'libxfixes')
makedepends=('mesa')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
source=("https://github.com/intel/libva/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('56ee129deba99b06eb4a8d4f746b117c5d1dc2ec5b7a0bfc06971fca1598ab9b')

build() {
  cd ${_pkgname}-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install-exec
  rm "${pkgdir}"/usr/lib/*.so
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
