# Maintainer:  twa022 <twa022 at gmail dot com>

_pkgname=libva
pkgname=lib32-${_pkgname}1
pkgver=1.8.3
pkgrel=2
pkgdesc='Video Acceleration (VA) API for Linux (32-bit) (Old version for Steam compatibility)'
arch=(x86_64)
url='https://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=(lib32-libdrm lib32-libgl lib32-wayland lib32-libxext lib32-libxfixes libva1)
makedepends=(mesa)
optdepends=('lib32-libva-vdpau-driver: vdpau back-end for nvidia'
            'lib32-libva-intel-driver: back-end for intel cards')
source=("https://github.com/intel/libva/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
sha512sums=('870cfaa5c4487f4cb4b01c11379484e3884aa5e9026c3836cc7681804a5e08a0b245b2aa788984a24a3094bd64068fbd0406e66cb4c5e30fc84e31095eb4a3e1')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --libdir='/usr/lib32' 
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-exec
  rm "${pkgdir}"/usr/lib32/*.so
  install -m644 -D COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
